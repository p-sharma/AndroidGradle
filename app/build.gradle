apply plugin: 'com.android.application'

apply from: "$rootDir/helper/signing/signing.gradle"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.demo.androidgradle"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        debug {
            signingConfig signingConfigs.defaultSigning
            manifestPlaceholders = [buildType: "dev", crashliticsApiKey: "YYYYYYYY"]
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.releaseSigning
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [buildType: "live", crashliticsApiKey: "XXXXXXX"]
        }
    }

    productFlavors {
        demo {
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }
        full {
            applicationIdSuffix ".release"
            versionNameSuffix "-release"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            println "Variant is ${variant.name} ${variant.versionName} ${variant.versionCode}"
            output.outputFile = new File(
                    output.outputFile.parent,
                    "App-${variant.name.replace("Debug", "").replace("Release", "")}" +
                            "-${variant.versionName}-b${variant.versionCode}-${buildNumber}.apk")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile libraries.appcompat
    testCompile libraries.junit
    androidTestCompile (libraries.espresso_core, {
      exclude group: 'com.android.support', module: 'support-annotations'
    })
}
